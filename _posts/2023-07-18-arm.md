---
layout:       post
title:        "arm机械臂指南"
author:       "谢峰"
header-style: text
catalog:      true
tags:
    - 2023
    - arm机械臂
---


## 代码实现

### File: Me_Arm_Big.ino

```arduino
//舵机命令符：b c f r
//状态显示符和初始化符：o i
/*
手柄模式

|     8                W
|  4  5  6          A  S  D
*/

#include<SoftwareSerial.h>   //蓝牙模块头文件
#include<Servo.h>
Servo base,fArm,rArm,claw;   //创建 4 个 servo 对象

//程序末尾有舵机初始化函数
//初始状态
int basePos = 90;
int rArmPos = 90;
int fArmPos = 90;
int clawPos = 90;

//舵机极限值
const int base_Min = 0;
const int base_Max = 180;
const int rArm_Min = 45;
const int rArm_Max = 180;
const int fArm_Min = 35;
const int fArm_Max = 120;
const int claw_Min = 25;
const int claw_Max = 100;

//Default Servo Delay (默认电机运动延时时间)
//此变量用于控制电机运行速度，【增大】此变量数值将【降低】电机运行速度从而控制机械臂转速
int DSD = 15; 

//控制机械臂工作模式 :  mode = 0 手柄模式 ，mode = 1 指令模式
bool mode;

//每次按下手柄按键，舵机移动量（仅用于手柄模式）
int move_Step = 3;

void setup() {

  //舵机连接的 PWM 引脚
  base.attach(11);
  delay(200);

  fArm.attach(10);
  delay(200);

  rArm.attach(9);
  delay(200);

  claw.attach(6);
  delay(200);

  //开启串口
  Serial.begin(9600);
  Serial.println("Welcome to use Arm ");
}

void loop() {

  //人机交互：传给 Arduino 数据
  if(Serial.available() > 0)
  {
    char serial_Cmd = Serial.read();   //输入的数据用 char 传入
    if(mode == 1)
    {
      arm_Data_Cmd(serial_Cmd);          //指令模式
    }
    else 
    {
      arm_Joy_Cmd(serial_Cmd);           //手柄模式
    }
  }  
} 

//指令模式-----------------------------------------------------------------------------------
void arm_Data_Cmd(char serial_Cmd)
{
  //判断是否输入有问题：在【指令】模式下输入【手柄】指令--错误
  if(serial_Cmd == 'W' || serial_Cmd == 'S' || serial_Cmd == 'A' || serial_Cmd == 'D' 
  || serial_Cmd == '8' || serial_Cmd == '5' || serial_Cmd == '4' || serial_Cmd == '6')
  {
    Serial.println("+Warning: robot in instruction mode.....");
    delay(100);
    while(Serial.available() > 0)  //清除串口缓存的错误指令
    {
      char wrong_Command = Serial.read();//将错误指令读取出来存放在一个变量中
    }
    return ;
  }

  //舵机命令符：b c f r
  if(serial_Cmd == 'b' || serial_Cmd == 'c' || serial_Cmd == 'f' || serial_Cmd == 'r')
  {
    int servo_Data = Serial.parseInt();        //这段数据中的 数字(角度信息)

    //机械臂舵机运行函数：(参数:舵机名称 , 目标角度 , 延迟/速度)
    servo_Cmd(serial_Cmd , servo_Data , DSD);  
  }
  else//状态显示符和初始化符：o i
  {
    switch(serial_Cmd)
    {
      case 'o':
      report_Status();//报告舵机状态数据
      break;

      case 'i':
      arm_Ini_Pos(); //输入 i 命令后使机械臂返回初始状态
      break;

      case 'p':
      play_Dice();   //输入 p 命令后使机械臂跳舞
      break;

      case 'm':      //切换手柄模式
      mode = 0;
      Serial.println("Command: Switch to instruction mode");
      break;

      default:
      Serial.println("Unkown Command.");
    }
  }
}

//手柄模式-----------------------------------------------------------------------------------
/*
手柄模式

|     8                W
|  4  5  6          A  S  D
*/
void arm_Joy_Cmd(char serial_Cmd)
{
  //判断是否输入有问题：在【手柄】模式下输入【指令】指令--错误
  if(serial_Cmd == 'b' || serial_Cmd == 'c' || serial_Cmd == 'f' || serial_Cmd == 'r')
  {
    Serial.println("+Warning: robot in instruction mode.....");
    delay(100);
    while(Serial.available() > 0)  //清除串口缓存的错误指令
    {
      char wrong_Command = Serial.read();//将错误指令读取出来存放在一个变量中
    }
    return ;
  }


  int base_Joy_Pos;  //底盘舵机：左 0 度 --- 右 180 度
  int rArm_Joy_Pos;  //后臂舵机：上 0 度 --- 下 180 度
  int fArm_Joy_Pos;  //前臂舵机：下 0 度 --- 上 180 度
  int claw_Joy_Pos;  //钳子舵机：合上 0 度 --- 打开 180 度

  switch(serial_Cmd)
  {
    case 'A': //base 向左转
    Serial.println("Receive Command: base turn Right");
    base_Joy_Pos = base.read() - move_Step;
    servo_Cmd('b' , base_Joy_Pos , DSD);
    break;

    case 'D': //base 向右转
    Serial.println("Receive Command: base turn Left");
    base_Joy_Pos = base.read() + move_Step;
    servo_Cmd('b' , base_Joy_Pos , DSD);
    break;

    case 'S': //rArm 向下转
    Serial.println("Receive Command: rArm turn Left");
    rArm_Joy_Pos = rArm.read() + move_Step;
    servo_Cmd('r' , rArm_Joy_Pos , DSD);
    break;

    case 'W': //rArm 向上转
    Serial.println("Receive Command: rArm turn Left");
    rArm_Joy_Pos = rArm.read() - move_Step;
    servo_Cmd('r' , rArm_Joy_Pos , DSD);
    break;

    case '4': //claw 钳子合上
    Serial.println("Receive Command: claw close");
    claw_Joy_Pos = claw.read() + move_Step;
    servo_Cmd('c' , claw_Joy_Pos , DSD);
    break;

    case '6': //claw 钳子打开
    Serial.println("Receive Command: claw open");
    claw_Joy_Pos = claw.read() - move_Step;
    servo_Cmd('c' , claw_Joy_Pos , DSD);
    break;

    case '5': //fArm 向下
    Serial.println("Receive Command: fArm open");
    fArm_Joy_Pos = fArm.read() - move_Step;
    servo_Cmd('f' , fArm_Joy_Pos , DSD);
    break;

    case '8': //fArm 向上
    Serial.println("Receive Command: fArm open");
    fArm_Joy_Pos = fArm.read() + move_Step;
    servo_Cmd('f' , fArm_Joy_Pos , DSD);
    break;

    case 'm': //切换指令模式
    mode = 1;
    Serial.println("Command: Switch to instruction mode");
    break;

    case 'o':
    report_Status(); //报告舵机状态数据
    break;

    case 'i':
    arm_Ini_Pos();  //输入 i 命令后使机械臂返回初始状态
    break;
  }
}


//程序末尾有舵机初始化函数
void servo_Cmd(char serial_Name , int to_Pos , int servo_Delay)
{
  //创建临时存放舵机
  Servo servo2go;

  //串口监视器输出接收指令信息
  Serial.println("");//和 Serial.print 一样，只不过有【换行功能】
  Serial.print("+Command: Servo ");
  Serial.print(" to ");
  Serial.print(serial_Name);
  Serial.print(" to ");
  Serial.print(to_Pos);
  Serial.print(" at servo_delay value ");
  Serial.print(servo_Delay);
  Serial.println(". ");
  Serial.println("");

  //建立变量，存储电机起始运动角度值
  int from_Pos;

  switch(serial_Name)
  {
    case 'b':
    if(to_Pos >= base_Min && to_Pos <= base_Max)                 //如果终止位置在极限范围内
    {
      servo2go = base;
      from_Pos = base.read();                                    //获取当前舵机角度值，作为初始运动角度值
      break;
    }
    else                                                         //如果终止位置 <= 起始位置
    {
      Serial.println("+Warning : base servo value out of limit!");
      return;//终止函数
    }

    case 'c':
    if(to_Pos >= claw_Min && to_Pos <= claw_Max)                 //如果终止位置在极限范围内
    {
      servo2go = claw;
      from_Pos = claw.read();                                    //获取当前舵机角度值，作为初始运动角度值
      break;
    }
    else                                                         //如果终止位置 <= 起始位置
    {
      Serial.println("+Warning : claw servo value out of limit!");
      return;
    }

    case 'r':
    if(to_Pos >= rArm_Min && to_Pos <= rArm_Max)                 //如果终止位置在极限范围内
    {
      servo2go = rArm;
      from_Pos = rArm.read();                                    //获取当前舵机角度值，作为初始运动角度值
      break;
    }
    else                                                         //如果终止位置 <= 起始位置
    {
      Serial.println("+Warning : rArm servo value out of limit!");
      return;
    }

    case 'f':
    if(to_Pos >= fArm_Min && to_Pos <= fArm_Max)                 //如果终止位置在极限范围内
    {
      servo2go = fArm;
      from_Pos = fArm.read();                                    //获取当前舵机角度值，作为初始运动角度值
      break;
    }
    else                                                         //如果终止位置 <= 起始位置
    {
      Serial.println("+Warning : fArm servo value out of limit!");
      return;
    }
  }

  //当 break; 语句结束后，将执行舵机运行
  //此时 from_Pos 存入了数据， to_Pos 也可以继续

  //舵机转向优化
    //********************************************************************
    if(from_Pos <= to_Pos)     //如果终止位置 >= 起始位置
    {
      //底盘舵机：从 from_Pos 到 to_Pos 的位置（以缓慢的速度）
      for(int i=from_Pos;i<=to_Pos;i++)
      {
        servo2go.write(i);
        delay(servo_Delay);
      }
    }
    else                       //如果终止位置 <= 起始位置
    {
      for(int i=from_Pos;i>=to_Pos;i--)
      {
        servo2go.write(i);
        delay(servo_Delay);
      }
    }
    //********************************************************************
}

void report_Status()
{
  Serial.println("");
  Serial.println("");
  Serial.println("+ Robot-Arm Status Report +");
  Serial.print("Claw Position: ");Serial.println(claw.read());
  Serial.print("Base Position: ");Serial.println(base.read());
  Serial.print("Rear-Arm Position: ");Serial.println(rArm.read());
  Serial.print("Front-Arm Position: ");Serial.println(fArm.read());
  Serial.println("++++++++++++++++++");
  Serial.println("");
}


//舞蹈函数
void play_Dice()
{
  Serial.println("+Command: Let's play some dice !");
  
  //初始化
  arm_Ini_Pos();

  //舞蹈 1
  int dice_Move_1[21][3]=
  {
    {'c' , 30 , DSD},
    {'b' , 90 , DSD},
    {'f' , 43 , DSD},
    {'r' , 126 , DSD},
    {'c' , 90 , DSD},
    {'r' , 90 , DSD},
    {'b' , 60 , DSD},
    {'f' , 36 , DSD},
    {'r' , 135 , DSD},
    {'c' , 30 , DSD},
    {'r' , 90 , DSD},
    {'b' , 90 , DSD},
    {'f' , 36 , DSD},
    {'r' , 135 , DSD},
    {'c' , 90 , DSD},
    {'r' , 90 , DSD},
    {'b' , 60 , DSD},
    {'f' , 48 , DSD},
    {'r' , 123 , DSD},
    {'c' , 30 , DSD},
    {'c' , 90 , DSD},
  };

  for(int i=0;i<21;i++)
  {
    servo_Cmd(dice_Move_1[i][0] , dice_Move_1[i][1] , dice_Move_1[i][2]);
    delay(200);
  }

  //初始化
  arm_Ini_Pos();

  //舞蹈 2
  int dice_Move_2[21][3]=
  {
    {'c' , 30 , DSD},
    {'b' , 58 , DSD},
    {'f' , 48 , DSD},
    {'r' , 120 , DSD},
    {'c' , 90 , DSD},
    {'r' , 90 , DSD},
    {'b' , 90 , DSD},
    {'f' , 36 , DSD},
    {'r' , 135 , DSD},
    {'c' , 30 , DSD},
    {'r' , 90 , DSD},
    {'b' , 60 , DSD},
    {'f' , 36 , DSD},
    {'r' , 135 , DSD},
    {'c' , 95 , DSD},
    {'r' , 90 , DSD},
    {'b' , 90 , DSD},
    {'f' , 43 , DSD},
    {'r' , 125 , DSD},
    {'c' , 30 , DSD},
    {'c' , 90 , DSD},
  };

  for(int i=0;i<21;i++)
  {
    servo_Cmd(dice_Move_2[i][0] , dice_Move_2[i][1] , dice_Move_2[i][2]);
    delay(200);
  }

  //初始化
  arm_Ini_Pos();
}


void arm_Ini_Pos()
{
  Serial.println("+Command: Restore Initial Position. ");

  //建立 4 个数组 = {舵机名 , 90度 , 延时15秒};
  // int action0[3] = {'b' , 90 , DSD};
  // int action1[3] = {'r' , 90 , DSD};
  // int action2[3] = {'f' , 90 , DSD};
  // int action3[3] = {'c' , 90 , DSD};

  //二维优化
  int robot_Ini_Pos_Array[4][3]=     //{舵机名 , 90度 , 延时15秒}
  {
    {'b' , 90 , DSD},
    {'r' , 90 , DSD},
    {'f' , 90 , DSD},
    {'c' , 90 , DSD}
  };

  // servo_Cmd(action0[0] , action0[1] , action0[2]);
  // servo_Cmd(action1[0] , action1[1] , action1[2]);
  // servo_Cmd(action2[0] , action2[1] , action2[2]);
  // servo_Cmd(action3[0] , action3[1] , action3[2]);

  //二维优化
  for(int i=0;i<4;i++)
  {
    servo_Cmd(robot_Ini_Pos_Array[i][0] , robot_Ini_Pos_Array[i][1] , robot_Ini_Pos_Array[i][2]);
  }
}



```


### File: arm_Ini_Pos.h

```arduino
/*
#ifndef _ARm_INI_POS_H_
#define _ARm_INI_POS_H_
#include<Arduino.h>

Servo base,fArm,rArm,claw;   //创建 4 个 servo 对象





#endif

*/
```


### File: arm_Ini_Pos.cpp

```arduino
/*

#include<arm_Ini_Pos.h>
void arm_Ini_Pos()
{
  Serial.println("+Command: Restore Initial Position. ");

  //建立 4 个数组 = {舵机名 , 90度 , 延时15秒};
  // int action0[3] = {'b' , 90 , DSD};
  // int action1[3] = {'r' , 90 , DSD};
  // int action2[3] = {'f' , 90 , DSD};
  // int action3[3] = {'c' , 90 , DSD};

  //二维优化
  int robot_Ini_Pos_Array[4][3]=     //{舵机名 , 90度 , 延时15秒}
  {
    {'b' , 90 , DSD},
    {'r' , 90 , DSD},
    {'f' , 90 , DSD},
    {'c' , 90 , DSD}
  };

  // servo_Cmd(action0[0] , action0[1] , action0[2]);
  // servo_Cmd(action1[0] , action1[1] , action1[2]);
  // servo_Cmd(action2[0] , action2[1] , action2[2]);
  // servo_Cmd(action3[0] , action3[1] , action3[2]);

  //二维优化
  for(int i=0;i<4;i++)
  {
    servo_Cmd(robot_Ini_Pos_Array[i][0] , robot_Ini_Pos_Array[i][1] , robot_Ini_Pos_Array[i][2]);
  }
}


*/
```


